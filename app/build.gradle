if (!isLibrary.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdk 34

    defaultConfig {
        if (!isLibrary.toBoolean()) {
            applicationId "com.moko.bxp.s"
        }
        namespace = "com.moko.bxp.s"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 102
        versionName "1.0.2"

        def formatDate = new Date().format("yyyyMMddHHmm")
        setProperty("archivesBaseName", "BXP_S_${versionName}_${formatDate}")
    }

    buildTypes {
        release {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
        debug {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    viewBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
        disable 'SetTextI18n'
        checkReleaseBuilds false
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    sourceSets {
        main {
            if (!isLibrary.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }
        }
    }

    if (isLibrary.toBoolean()) {
        libraryVariants.all { variant ->
            if (variant.buildType.name == "release") {
                // 修改aar名称
                variant.outputs.all { output ->
                    def formatDate = new Date().format("yyyyMMddHHmm")
                    outputFileName = "BXP_S_${android.defaultConfig.versionName}_${formatDate}.aar"
                }
                // 复制aar到指定目录
                variant.assemble.doLast {
                    variant.outputs.all { output ->
                        // 输出目录在：项目目录/build/outputs/aar/
                        def outputPath = [project.rootDir.absolutePath, "build", "outputs", "aar"].join(File.separator)
                        copy {
                            from output.outputFile
                            into outputPath
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    implementation 'com.github.GrenderG:Toasty:1.4.1'
    implementation 'cn.carbswang.android:NumberPickerView:1.1.1'
    implementation 'com.guolindev.permissionx:permissionx:1.7.1'
    implementation files('libs/jxl.jar')
    if (!isLibrary.toBoolean()) {
        implementation project(path: ':mokosupport')
        implementation(name: 'pickerview', ext: 'aar')
        implementation(name: 'wheelview', ext: 'aar')
    } else {
        implementation files('libs/mokoSupport.jar')
        compileOnly files('libs/mokoBleLib.jar')
        compileOnly(name: 'pickerview', ext: 'aar')
        compileOnly(name: 'wheelview', ext: 'aar')
        implementation 'no.nordicsemi.android.support.v18:scanner:1.6.0'
        implementation 'org.greenrobot:eventbus:3.2.0'
        implementation 'com.elvishew:xlog:1.6.1'
        implementation 'no.nordicsemi.android:ble:2.7.3'
    }
}
