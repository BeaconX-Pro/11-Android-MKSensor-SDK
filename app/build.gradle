if (!isLibrary.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdk 34
    namespace "com.moko.bxp.s"

    defaultConfig {
        minSdk 26
        targetSdk 34
        versionCode 104
        versionName "1.0.4"

        def formatDate = new Date().format("yyyyMMddHHmm")
        setProperty("archivesBaseName", "BXP_S_${versionName}_${formatDate}")
    }

    buildTypes {
        release {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
        debug {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    viewBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
        disable 'SetTextI18n'
        checkReleaseBuilds false
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    sourceSets {
        main {
            if (!isLibrary.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }
        }
    }

    if (isLibrary.toBoolean()) {
        libraryVariants.all { variant ->
            if (variant.buildType.name == "debug") {
                // 修改aar名称
                variant.outputs.all { output ->
                    def formatDate = new Date().format("yyyyMMddHHmm")
                    outputFileName = "BXP_S_${android.defaultConfig.versionName}_${formatDate}.aar"
                }
                // 复制aar到指定目录
                variant.assemble.doLast {
                    variant.outputs.all { output ->
                        // 输出目录在：项目目录/build/outputs/aar/
                        def outputPath = [project.rootDir.absolutePath, "build", "outputs", "aar"].join(File.separator)
                        copy {
                            from output.outputFile
                            into outputPath
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation files('libs/jxl.jar')
    if (!isLibrary.toBoolean()) {
        implementation project(path: ':mokosupport')
        implementation 'com.github.MOKO-Android-Base-Library:MKBXPUILib:1.0.3'
        implementation(name: 'pickerview', ext: 'aar')
        implementation(name: 'wheelview', ext: 'aar')
    } else {
        implementation files('libs/mokoSupport.jar')
        compileOnly 'com.github.MOKO-Android-Base-Library:MKBXPUILib:1.0.3'
        compileOnly 'com.github.MOKO-Android-Base-Library:MKBleLib:1.0.0-beacon'
        compileOnly(name: 'pickerview', ext: 'aar')
        compileOnly(name: 'wheelview', ext: 'aar')
    }
}
